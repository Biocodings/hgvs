hgvs_variant = c_variant | g_variant

############################################################################
# c.
c_variant = c_accn:ac ':' c_type:type '.' c_interval:interval c_edit:edit -> Variant(ac,type,interval,edit)
c_accn = accn
c_type = 'c'
c_interval = interval
c_edit = dna_subst | dna_delins | dna_ins | dna_del | dna_dup

############################################################################
# g.
g_variant = g_accn:ac ':' g_type:type '.' g_interval:interval g_edit:edit -> Variant(ac,type,interval,edit)
g_accn = accn
g_type = :x ?(x in 'gm') -> x
g_interval = interval
g_edit = dna_subst | dna_delins | dna_ins | dna_del | dna_dup

############################################################################
# x_variant: lax parser with minimal syntactic validation
x_variant = accn:ac ':' x_type:type '.' x_interval:interval x_edit:edit -> Variant(ac,type,interval,edit)
x_ac = <( ~':' anything )+>
x_type = :x ?(x in 'cgmpr') -> x
x_interval = <interval | pos>
x_edit = <anything+>


############################################################################
## Edits

dna_del = 'del' (<num>|<dna*>):pre -> DelIns(pre=pre,post=None)
dna_delins = 'del' (<num>|<dna*>):pre 'ins' <dna+>:post  -> DelIns(pre=pre,post=post)
dna_dup = 'dup' <dna*>:seq -> Dup(seq=seq)
dna_ins = 'ins' <dna+>:post -> DelIns(pre=None,post=post)
dna_subst = dna:pre '>' dna:post -> DelIns(pre=pre,post=post)


############################################################################
## Basic types

dna_seq = <dna+>
dna = :x ?(x in 'ACGT') -> x

rna_seq = <rna+>
rna = :x ?(x in 'ACGU') -> x

aa1_seq = <aa1+>
aa1 = :x ?(x in 'ACDEFGHIKLMNPQRSTVWY') -> x

aa3_seq = <aa3+>
aa3 = :x ?(x in ['Ala','Cys','Asp','Glu','Phe','Gly','His','Iso','Lys','Met','Asn','Pro','Gln','Arg','Ser','Thr','Val','Trp','Tyr']) -> x

c_interval = (c_pos:start '_' c_pos:end -> Interval(start,end)) | (c_pos:start -> Interval(start,start))
c_pos = ('*' num:x -> CDSPosition(0,x)) 
		| ('-' num:x -> CDSPosition(0,-x))
		| (num:b (snum | -> 0):o -> CDSPosition(b,o))

interval = (pos:start '_' pos:end -> Interval(start,end)) | (pos:start -> Interval(start,start))
pos = num:pos -> Position(pos)

accn = <letter letterOrDigit+ ('_' letterOrDigit+)? ('.' digit+)>

snum = <pm? num>:x -> int(x)
num = <digit+>:x -> int(x)
pm = :x ?(x in '-+') -> x
